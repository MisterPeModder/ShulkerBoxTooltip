plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'com.modrinth.minotaur' version '2.+'
}

archivesBaseName = "${rootProject.projectArchiveBaseName}-fabric"

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    accessWidenerPath = project(':common').loom.accessWidenerPath
}

configurations {
    common
    shadowCommon
    // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabricLoaderVersion}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabricApiVersion}"

    // Mod Menu
    modImplementation("com.terraformersmc:modmenu:${rootProject.modMenuVersion}") {
        exclude(group: 'net.fabricmc.fabric-api')
    }

    // Cloth-Config
    modImplementation("me.shedaniel.cloth:cloth-config-fabric:${rootProject.clothConfigVersion}") {
        exclude(group: 'net.fabricmc.fabric-api')
    }
    include "me.shedaniel.cloth:cloth-config-fabric:${rootProject.clothConfigVersion}"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowCommon(project(path: ':common', configuration: 'transformProductionFabric')) { transitive false }

    // Nullable annotations.
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'org.jetbrains:annotations:23.0.0'
}

processResources {
    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand 'version': project.version
    }
}

shadowJar {
    exclude 'architectury.common.json'

    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set('dev-shadow')
}

remapJar {
    injectAccessWidener = true
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set(null)
}

jar {
    archiveClassifier.set('dev')
}

sourcesJar {
    def commonSources = project(':common').sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

// Include both common and fabric documentation in the javadoc
def javadocProjects = [':fabric', ':common']
javadoc {
    source = javadocProjects.collect { project(it).sourceSets.main.allJava }
    classpath = files(javadocProjects.collect { project(it).sourceSets.main.compileClasspath })

    exclude 'com/misterpemodder/shulkerboxtooltip/impl/**'
    exclude 'com/misterpemodder/shulkerboxtooltip/mixin/**'
    exclude 'com/misterpemodder/shulkerboxtooltip/*.java'
    exclude 'com/misterpemodder/shulkerboxtooltip/fabric/*.java'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            groupId = project.group
            artifactId = project.archivesBaseName
            version = project.version

            artifact(remapJar) {
                builtBy remapJar
            }

            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }

            artifact(javadocJar) {
                builtBy javadocJar
            }

            pom rootProject.ext.makePom(project)
        }
    }
}

signing {
    def signingKey = findProperty('signingKey')
    def signingPassword = findProperty('signingPassword')
    if (signingKey && signingPassword) {
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.mavenFabric
    } else {
        println("${project.name}: skipping jar signing")
    }
}

tasks.curseforge.enabled = System.getenv('CURSE_API_KEY') && project.hasProperty('curseProjectId')

curseforge {
    if (System.getenv('CURSE_API_KEY'))
        apiKey = System.getenv('CURSE_API_KEY')

    project {
        id = project.curseProjectId
        changelog = rootProject.ext.parseChangelog('changelog.txt')
        releaseType = rootProject.getReleaseType()
        for (gameVersion in project.hostGameVersions.split(',')) {
            addGameVersion gameVersion
        }
        addGameVersion 'Fabric'
        addGameVersion 'Java 17'
        mainArtifact(remapJar) {
            displayName = "[Fabric $project.mcVersionFull] v${project.version}"
        }
        relations {
            requiredDependency 'fabric-api'
            embeddedLibrary 'cloth-config'
            optionalDependency 'modmenu'
        }
    }
    options {
        forgeGradleIntegration = false
        javaVersionAutoDetect = false
        debug = project.projectBaseVersion.contains('SNAPSHOT')
    }
}

afterEvaluate {
    // Manually set dependsOn because curseforge doesn't set one.
    tasks."curseforge${project.curseProjectId}".dependsOn remapJar
}

tasks.modrinth.enabled = System.getenv('MODRINTH_TOKEN') && project.hasProperty('modrinthProjectId')

modrinth {
    token = System.getenv('MODRINTH_TOKEN')
    projectId = modrinthProjectId
    versionNumber = "${project.version}-fabric"
    versionName = "[Fabric $project.mcVersionFull] v${project.version}"
    versionType = project.getReleaseType()
    changelog = rootProject.parseChangelog('changelog.txt')
    uploadFile = remapJar
    gameVersions = hostGameVersions.split(',').toList()
    loaders = ['fabric']
    debugMode = project.projectBaseVersion.contains('SNAPSHOT')
    dependencies {
        required.project 'P7dR8mSH' // fabric-api
        optional.project 'mOgUt4GM' // modmenu
        embedded.project '9s6osm5g' // cloth-config-api
    }
}
