archivesBaseName = "${rootProject.projectArchiveBaseName}-common"

architectury {
    common(rootProject.enabledPlatforms.split(','))
}

loom {
    accessWidenerPath = file('src/main/resources/shulkerboxtooltip.accesswidener')
}

dependencies {
    // We depend on fabric loader here to use the fabric @Environment annotations and get the mixin dependencies
    // Do NOT use other classes from fabric loader
    modImplementation("net.fabricmc:fabric-loader:${rootProject.fabricLoaderVersion}")

    // Cloth-Config
    modApi("me.shedaniel.cloth:cloth-config:${rootProject.clothConfigVersion}")

    // Nullable annotations.
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'org.jetbrains:annotations:23.0.0'
}

javadoc {
    exclude 'com/misterpemodder/shulkerboxtooltip/impl/**'
    exclude 'com/misterpemodder/shulkerboxtooltip/mixin/**'
    exclude 'com/misterpemodder/shulkerboxtooltip/*.java'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenCommon(MavenPublication) {
            groupId = project.group
            artifactId = project.archivesBaseName
            version = project.version

            artifact(remapJar) {
                builtBy remapJar
            }

            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }

            artifact(javadocJar) {
                builtBy javadocJar
            }

            pom rootProject.ext.makePom(project)
        }
    }
}

signing {
    def signingKey = findProperty('signingKey')
    def signingPassword = findProperty('signingPassword')
    if (signingKey && signingPassword) {
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.mavenCommon
    } else {
        println("${project.name}: skipping jar signing")
    }
}
