plugins {
	id 'fabric-loom' version '0.2.1-SNAPSHOT'
	id 'signing'
	id 'maven-publish'
	id 'com.matthewprenger.cursegradle' version "1.1.2"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = projectGroup
archivesBaseName = projectArchiveBaseName
version = "$projectVersion+$mcVersion" + (isSnapshot.equals("true") ?
	"-SNAPSHOT" : "")

repositories {
	mavenLocal()
	maven {
    url = 'https://maven.jamieswhiteshirt.com/libs-release/'
  }
	maven {
		name = 'CurseForge'
		url 'https://minecraft.curseforge.com/api/maven'
	}
}

dependencies {
	minecraft "com.mojang:minecraft:$mcVersion"
	mappings "net.fabricmc:yarn:$mcVersion.$mcMappings"
	modCompile "net.fabricmc:fabric-loader:$fabricLoaderVersion"
	modCompile "net.fabricmc:fabric:$fabricApiVersion"

	// modCompile "com.jamieswhiteshirt:developer-mode:1.0.3"

	// Mod Menu
	modCompile "io.github.prospector.modmenu:ModMenu:$modMenuVersion"

	// Cloth-Configw
	modCompile "cloth-config:ClothConfig:$clothConfigVersion"
	include "cloth-config:ClothConfig:$clothConfigVersion"

	// Nullable annotations.
	compile "com.google.code.findbugs:jsr305:3.0.0"
}

apply from: 'https://github.com/MisterPeModder/common-docs/raw/master/gradle/fabric-mod.gradle'
apply from: 'https://github.com/MisterPeModder/common-docs/raw/master/gradle/publishing.gradle'

String parseChangelog(String path) {
	def file = new File(path)
	if (!file.exists())
		return '(no changelog)'
	String changelog = ""
	boolean end = false
	int line = 0
	file.eachLine {
		if (end || it == null)
			return
		++line
		if (!it.startsWith('+=+=+'))
			changelog += "$it\n"
		else if (line > 3)
			end = true
	}
	return changelog
}

curseforge {
  if (project.hasProperty('curseApiKey')
      && project.hasProperty('curseProjectId')
      && project.hasProperty('curseGameVersion')) {
    apiKey = curseApiKey
	  project {
	    id = curseProjectId
	    changelog = parseChangelog('changelog.txt')
	    releaseType = isSnapshot.equals("true") ? 'alpha' : 'release'
	    addGameVersion curseGameVersion
	    mainArtifact(jar) {
	    	displayName = "$projectName-${project.version}"
	    }
	    relations {
        requiredDependency 'fabric'
      }
	  }
	  options {
	  	forgeGradleIntegration = false
	  }
  } else {
    println("curseforge: missing required property: 'curseApiKey'," +
      "'curseProjectId' or 'curseGameVersion'")
  }
}

afterEvaluate {
	// Manually set dependsOn because curseforge doesn't set one.
  tasks."curseforge$curseProjectId".dependsOn remapJar
}